#!/usr/bin/env perl

# Pragmas.
use strict;
use warnings;

# Modules.
use PYX::Parser;

# Tags, that are simple.
our @TAGS = ('meta', 'link', 'br', 'hr', 'img', 'input', 'option');

# Debug.
our $DEBUG = 0;

# Stack.
our @stack;

# Arguments.
if (@ARGV < 1) {
	print STDERR "Usage: $0 [filename] [-]\n";
	print STDERR "\tfilename\tprocess on filename\n";
	print STDERR "\t-\t\tprocess on stdin\n";
	exit 1;
}
my $filename_or_stdin = $ARGV[0];

# PYX::Parser object.
my $pyx_parser = PYX::Parser->new(
	'data' => \&_data,
	'end_tag' => \&_end_tag,
	'output_rewrite' => 1,
	'start_tag' => \&_start_tag,
);

# Parse from stdin.
if ($filename_or_stdin eq '-') {
	$pyx_parser->parse_handler(\*STDIN);

# Parse file.
} else {
	$pyx_parser->parse_file($filename_or_stdin);
}

# Process start tag.
sub _start_tag {
	my $obj = shift;
	my $tag = shift;
	_single();
	if ($DEBUG) {
		print 'push: ';	
	}
	push @stack, $tag;
	if ($DEBUG) {
		print "$tag\n";
	}
	print $obj->{'line'}, "\n";
}

# Process end tag.
sub _end_tag {
	my $obj = shift;
	_single();
	_pop_stack();
	print $obj->{'line'}, "\n";
}

# Process data.
sub _data {
	my $obj = shift;
	_single();
	print $obj->{'line'}, "\n";
}

# Process end of single.
sub _single {
	my $tag = $stack[-1];
	if ($tag && grep(m/^$tag$/ms, @TAGS)) {
		print ')'.$tag."\n";
		_pop_stack();
	}
}

# Pop stack helper.
sub _pop_stack {
	if ($DEBUG) {
		print 'pop: ';
	}
	my $x = pop @stack;
	if ($DEBUG) {
		print "$x\n";
	}
	return;
}
